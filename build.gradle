plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group project.maven_group
version project.artifact_version

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation(
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
            'org.postgresql:postgresql',
            'org.projectlombok:lombok'
    )
    annotationProcessor (
            'org.springframework.boot:spring-boot-configuration-processor',
            'org.projectlombok:lombok'
    )
    testImplementation (
            "org.junit.jupiter:junit-jupiter-api:5.9.0",
            'org.springframework.boot:spring-boot-starter-test',
            'org.mockito:mockito-inline:4.8.0',
    )
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

check {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    finalizedBy jacocoTestCoverageVerification
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

/* JACOCO */
jacocoTestReport {
    reports {
        xml.enabled(true)
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                // minimum = 0.90
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                // minimum = 0.80
            }
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                // maximum = 200
            }
            excludes = [
                    // "com.example.somewhere.*"
            ]
        }
    }
}

//----------------------------------------------------
// PUBLISH
//----------------------------------------------------
publishing {
    // Declare all publications
    publications {
        mavenJava(MavenPublication) { from components.java }
    }

    // Add repositories to publish to
    repositories {
        // GitHub Packages (https://pkg.github.com)
        maven {
            name 'GitHub'
            url "https://maven.pkg.github.com/${project.github_repo}"
            credentials {
                username System.getenv('GITHUB_ACTOR')
                password System.getenv('GITHUB_TOKEN')
            }
        }
    }
}
